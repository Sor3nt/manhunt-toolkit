SCRIPTMAIN Batch1Script;

ENTITY
HUNTER1	:	et_name;

VAR
levelstatus	: level_var integer;

vec	: Vec3D;

SCRIPT OnCreate;
BEGIN
AIAddEntity(GetEntityName(this));

{IDENTITY}
AISetEntityVoiceID(GetEntityName(this),VOICE_INNOCENT3VOICE1);
SetHunterSkin(this,0);
SetHunterHeadSkin(this,0);

IF levelstatus = 0 THEN 
	BEGIN
	AIAddHunterToLeaderSubpack('LEADER','subMall2',GetEntityName(this));
	AIDefineGoalGotoNode('goalGOTO1',GetEntityName(this),AISCRIPT_HIGHPRIORITY,'coverFH1',AISCRIPT_RUNMOVESPEED,TRUE);
	
	SetVector(vec,0.7,0.0,0.7);
	AISetHunterHomeNodeDirection(GetEntityName(this),'homehunter1',vec);
	END
	
ELSE 
	BEGIN
		AIAddHunterToLeaderSubpack('LEADER','subHuntPack',GetEntityName(this));
		AISetHunterIdleActionMinMaxRadius(GetEntityName(this),AISCRIPT_IDLE_WANDERSEARCH,AISCRIPT_HIGHPRIORITY,140,200,20);
	END;
		
SetHunterHideHealth(GetEntityName(this),0);
SetHunterDropAmmo(this,4);

AIIgnoreEntityIfDead(GetEntityName(this));
END;


SCRIPT OnLowSightingOrAbove; {only used if hunter 2 or 3 see the player while they are stood at the barrel}
BEGIN
IF levelstatus = 0 THEN RunScript('LEADER','StartsubMall2');
RemoveThisScript;
END;

SCRIPT OnBeingShot; {only used if player shoots hunter 2 or 3 while they are stood idle at the barrel}
BEGIN
IF levelstatus = 0 THEN RunScript('LEADER','StartsubMall2');
RemoveThisScript;
END;

SCRIPT StartHunt;
BEGIN
AIAddGoalForSubpack('LEADER','subMall2','goalGOTO1');
AISetIdleHomeNode(GetEntityName(this),'');
AISetHunterIdleActionMinMaxRadius(GetEntityName(this),AISCRIPT_IDLE_WANDERSEARCH,AISCRIPT_HIGHPRIORITY,300,300,6);
RemoveThisScript;
END;

SCRIPT OnGuardIdle;
BEGIN
IF levelstatus = 0 THEN RunScript(GetEntityName(this),'Wander');
END;

SCRIPT Wander;
BEGIN
sleep(9000);
IF IsEntityAlive(GetEntityName(this)) THEN
	BEGIN 
		IF (AIIsEntityGuard(GetEntityName(this)) AND AIIsGuardIdle(GetEntityName(this))) OR (AIIsIdle(GetEntityName(this))) THEN RunScript('LEADER','WanderCheckB1');
	END;
END;

SCRIPT OnMediumSightingOrAbove;
BEGIN
RunScript('LEADER','PlayerSpotted');
END;

SCRIPT MoveToHuntPack;
BEGIN
IF IsEntityAlive(GetEntityName(this)) THEN AIRemoveHunterFromLeaderSubpack('LEADER',GetEntityName(this));
IF IsEntityAlive(GetEntityName(this)) THEN AIAddHunterToLeaderSubpack('LEADER','subHuntPack',GetEntityName(this));
END;

SCRIPT OnDeath;
BEGIN
IF AIIsGoalNameInSubpack('LEADER','subMall2','goalCOVERFH1') THEN AIRemoveGoalFromSubpack('LEADER','subMall2','goalCOVERFH1');
END;

END.
SCRIPTMAIN	HunterScripts;

ENTITY
	Guard13		:	et_name;


VAR
bInAlertPosition,	
bIsPlayingAnim		: boolean;

{LEVEL SCOPED VARS}
level_bIsAlertOn	: level_var boolean;
level_bAreCamerasOn	: level_var boolean;
bMapNotAquired		: level_var boolean;
sLastDeadHunter		: level_var string[32];

SCRIPT OnCreate;
BEGIN
	
	AISetEntityAllowSurprise(getEntityName(this),FALSE);
	SetHunterHideHealth(getEntityName(this),0);	
		
	AISetHunterIdleActionMinMax(getEntityName(this),AISCRIPT_IDLE_STANDSTILL,AISCRIPT_MEDIUMPRIORITY,5,10);
	AISetHunterIdleAction(getEntityName(this),AISCRIPT_IDLE_SPEECH,AISCRIPT_LOWPRIORITY);
	
	AISetEntityIdleOverride('Guard13',TRUE,TRUE);
	AIEntityPlayAnimLooped('Guard13','IdleLoop_Cerb_Sit',0.0);
	bIsPlayingAnim := TRUE;
End;


SCRIPT OnHighSightingOrAbove;
BEGIN
	
	if bIsPlayingAnim then
	begin
		AIEntityCancelAnim('Guard13','IdleLoop_Cerb_Sit');
		AIEntityPlayAnim('Guard13','Idle_Cerb_Stand');
		sleep(1000);
		AISetEntityIdleOverride('Guard13',FALSE,FALSE);			
		bIsPlayingAnim := FALSE;
	end
	else AISetEntityIdleOverride('Guard13',FALSE,FALSE);		
	
	if NOT level_bIsAlertOn then runscript('Alert_Manager','RaiseAlert')
	else runscript('Alert_Manager','AreaCheck');
	
	If (AINumberInSubPack('LEADER','CAMERA_ENFORCERS') <= 1) then {if he is alone...}
	begin
		
		If AIIsGoalNameInSubPack('LEADER','CAMERA_ENFORCERS','gCameraGuy')
			then AIRemoveGoalFromSubPack('LEADER','CAMERA_ENFORCERS','gCameraGuy');
			
	end;
		
End;


SCRIPT OnHighHearingOrAbove;
BEGIN

	{Check if the player is holding a gun, it means the guard has heard a definite gunshot}
			
	if IsEntityHoldingAmmoWeapon(GetPlayer) then
	begin
		if NOT level_bIsAlertOn	then runscript('Alert_Manager','RaiseAlert')					
		else Runscript('Alert_Manager','AreaCheck');		
	end;
	
	If (AINumberInSubPack('LEADER','CAMERA_ENFORCERS') = 1) then {if he is alone...}
	begin

		if bIsPlayingAnim then
		begin
			AIEntityCancelAnim('Guard13','IdleLoop_Cerb_Sit');
			AIEntityPlayAnim('Guard13','Idle_Cerb_Stand');	
			sleep(1000);
			AISetEntityIdleOverride('Guard13',FALSE,FALSE);
			bIsPlayingAnim := FALSE;
		end
		else AISetEntityIdleOverride('Guard13',FALSE,FALSE);

		If AIIsGoalNameInSubPack('LEADER','CAMERA_ENFORCERS','gCameraGuy')
			then AIRemoveGoalFromSubPack('LEADER','CAMERA_ENFORCERS','gCameraGuy');
			
	end;

	
End;

SCRIPT OnHunterIdle;
BEGIN
	if level_bIsAlertOn then runscript('Alert_Manager','WaitToResetAlert');
End;
	
SCRIPT OnDeath;
BEGIN
	If bMapNotAquired then runscript('Mansion_Int','InitMap');
	
	{This is a special Guard, the monitor guy, so when he dies the cameras are OFF}
	level_bAreCamerasOn := FALSE;
	sLastDeadHunter := GetEntityName(this);
	if level_bIsAlertOn then Runscript('Alert_Manager','DetectCamping');
End;

END. {end of Scripts}